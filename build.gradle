plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '2.2.3'
}

group 'me.gregzee.welcomer'
version '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.codemc.io/repository/maven-releases/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation "net.kyori:adventure-platform-bukkit:4.3.2"
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

tasks {
    shadowJar {
        minimize()
        archiveFileName.set("${project.name}-${project.version}.jar")

        relocate('net.kyori', 'me.gregzee.welcomer.shaded.kyori')
        relocate('co.aikar.commands', 'me.gregzee.welcomer.shaded.acf')
        relocate('co.aikar.locales', 'me.gregzee.welcomer.shaded.locales')
        relocate('com.google.gson', 'me.gregzee.welcomer.shaded.gson')
        relocate('org.bstats', 'me.gregzee.welcomer')
    }

    build {
        dependsOn(shadowJar)
    }

    runServer {
        // The version of the server to run
        def version = "1.8.8"

        minecraftVersion(version)
        runDirectory.set(file("server/$version"))

        // 1.8.8 - 1.16.5 = Java 8
        // 1.17           = Java 16
        // 1.18 - 1.20.4  = Java 17
        javaLauncher.set(project.javaToolchains.launcherFor {
            languageVersion.set(JavaLanguageVersion.of(8))
        })

        downloadPlugins {
            url("https://github.com/EssentialsX/Essentials/releases/download/2.20.1/EssentialsX-2.20.1.jar")
            url("https://ci.lucko.me/job/spark/400/artifact/spark-bukkit/build/libs/spark-1.10.59-bukkit.jar")
            url("https://download.luckperms.net/1530/bukkit/loader/LuckPerms-Bukkit-5.4.117.jar")
        }

        jvmArgs = [
                "-Dcom.mojang.eula.agree=true"
        ]
    }
}